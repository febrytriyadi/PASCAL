0 CLS:DIMX,Y,A(31),J(30,15),PX,PY,C(8,2),P(8,2),K(255),V,XM,YM,NX,NY,PY,PX,EM,UP$,DN$,RT$,LT$:GOTO30
8 XM=0:RETURN
9 YM=0:RETURN
30 GOSUB210:GOSUB340
40 CLS:PRINT@6*32,;:A$="RAOUL BEGINS HIS QUEST":GOSUB138:PRINT:A$="DEEP IN THE JUNGLE...":GOSUB138
45 GOSUB405:CLS0:GOSUB180
49 REM MAIN LOOP
50 GOSUB580:IFLV<>5THENIFWI=0THENGOSUB520
53 GOSUB460:GOTO50
126 REM BLOOD
130 FORX=1TO200:Y=RND(32)-1:IFA(Y)<15THENPRINT@32*A(Y)+Y,"¿";:A(Y)=A(Y)+1:NEXT:RETURN
134 NEXT:RETURN
136 REM CENTER (A$)
138 PRINTTAB(INT((32-LEN(A$))/2));A$:RETURN
139 REM CHECKCONTACT
140 POKE16384+32*C(X,2)+C(X,1),EM:POKEV,CA:MD=1:GOTO700
145 POKE16384+32*P(X,2)+P(X,1),EM:POKEV,PL:MD=2:GOTO700
146 POKE16384+32*PY+PX,EM:POKEV,CA:MD=1:GOTO700
147 POKE16384+32*PY+PX,EM:POKEV,PL:MD=2:GOTO700
149 REM DRAWSCREEN
180 FORX=1TO30:FORY=1TO15:POKE16384+32*Y+X,J(X,Y):NEXT:NEXT
190 POKE16384+32*PY+PX,RA
192 FORX=1TO8:IFC(X,1)<>0THENPOKE16384+32*C(X,2)+C(X,1),CA
198 IFP(X,1)<>0THENPOKE16384+32*P(X,2)+P(X,1),PL
203 NEXTX:PRINT@0,"SCORE"SC:PRINT@13,"AMMO"AM:PRINT@25,"GUNS"GG;:RETURN
209 REM INITIALIZE
210 C1=RND(-(PEEK(9)*256+PEEK(10)))
211 RA=24:EM=32:TR=30:BU=35:WA=223:RU=219:PL=37:CA=0:JE=42:AB=15:GU=31:GO=36:ID=38
212 K(RA)=1:K(EM)=2:K(TR)=3:K(BU)=4:K(WA)=5:K(RU)=6:K(PL)=7:K(CA)=8:K(JE)=9:K(AB)=10:K(GU)=11:K(GO)=12:K(ID)=13
213 K(88)=1:K(32)=2:K(84)=3:K(66)=4:K(87)=5:K(82)=6:K(80)=7:K(67)=8:K(74)=9:K(65)=10:K(71)=11:K(36)=12:K(73)=13
215 UP$="W":DN$="Z":RT$="S":LT$="A":LV=1:RETURN
225 Y=((PEEK(17024)AND1)*256+PEEK(17025))+16384:PRINT" ";:POKEY,X:RETURN
229 REM SUB INSTRUCTIONS
230 CLS
233 X=RA:GOSUB225:PRINT": I AM JUNGLE RAOUL, THE"
238 PRINT"FEARLESS EXPLORER."
240 PRINT"YOU CAN MOVE ME AROUND USING"
245 PRINT"THE ARROW KEYS (awsz)."
246 PRINT"PRESS THE space KEY AND I WILL"
251 PRINT"STAND STILL FOR A TURN, LETTING"
252 PRINT"THE ENEMIES MOVE."
255 PRINT"PRESS m, FOLLOWED BY AN ARROW"
257 PRINT"KEY, AND I WILL SWING MY TRUSTY"
258 PRINT"MACHETE, DESTROYING ANY BUSHES"
259 PRINT"OR CARNIVOROUS PLANTS IN MY WAY.";
261 PRINT"PRESS f, FOLLOWED BY AN ARROW"
264 PRINT"KEY, AND I WILL FIRE MY GUN (IF,";
265 PRINT"OF COURSE, I POSESS A GUN,)"
266 PRINT"WHICH WILL KILL ANY ENEMY."
268 GOSUB570
270 CLS
271 X=TR:GOSUB225:PRINT": TREES (IMPASSABLE)"
275 X=BU:GOSUB225:PRINT": BUSHES-- THESE CAN BE HACKED"
277 PRINT"AWAY WITH ONE SWIPE OF MY":PRINT"MACHETE."
279 X=WA:GOSUB225:PRINT": WALLS OR RUINS (IMPASSABLE)"
283 X=GU:GOSUB225:PRINT",";:X=AB:GOSUB225
287 PRINT": GUN & AMMO BOX (3 SHOTS)."
290 X=GO:GOSUB225:PRINT",";:X=JE:GOSUB225
294 PRINT": GOLD & JEWELS-- BONUS PTS"
297 X=PL:GOSUB225
299 PRINT": CARNIVOROUS PLANTS. THESE"
300 PRINT"CAN BE KILLED WITH EITHER A"
301 PRINT"MACHETE OR A GUNSHOT."
304 X=CA:GOSUB225:PRINT": CANNIBALS. THEY CAN BE"
307 PRINT"KILLED ONLY BY A GUNSHOT."
310 X=ID:GOSUB225:PRINT": THE SAPPHIRE AMPERSAND--"
313 PRINT "THE OBJECT OF YOUR QUEST..."
314 GOSUB570
316 CLS
318 A$="HINTS:":GOSUB138
321 PRINT"ONCE YOU HAVE KILLED ALL OF THE"
322 PRINT"ENEMIES ON A LEVEL, YOU WILL"
323 PRINT"MOVE ON TO THE NEXT LEVEL."
325 PRINT"YOU ONLY HAVE ONE LIFE"
326 PRINT"-- MAKE IT COUNT."
327 PRINT"AMMO NOT USED ON ONE LEVEL"
328 PRINT"CARRIES OVER INTO THE NEXT."
329 PRINT"DON'T WASTE IT, BECAUSE YOU'LL"
330 PRINT"NEED IT."
331 PRINT"DON'T WASTE BULLETS ON THE"
332 PRINT"PLANTS-- THEY ARE EASILY "
333 PRINT"DISPATCHED WITH YOUR MACHETE."
335 GOSUB570:RETURN
340 CLS:PRINT
350 A$="/////////"+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92):GOSUB138
352 A$="//////////"+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92):GOSUB138
354 A$=" ! J U N G L E    !":GOSUB138
356 A$=" !                !":GOSUB138
358 A$=" !      R A O U L !":GOSUB138
360 A$=" !----------------!":GOSUB138:PRINT
363 A$="IN":GOSUB138:PRINT
366 A$="THE QUEST":GOSUB138
368 A$="FOR THE":GOSUB138
370 A$="SAPPHIRE AMPERSAND (&)":GOSUB138:PRINT
372 A$="(P)LAY (I)NSTRUCTIONS (Q)UIT":GOSUB138
396 A$=INKEY$:IFA$=""THEN396
397 IFA$="P"THENCLS:RETURN
398 IFA$="I"THENGOSUB230:GOTO340
399 IFA$="Q"THENEND
400 GOTO396
402 REM MAKELEVEL
405 FORX=1TO8:P(X,1)=0:P(X,2)=0:C(X,1)=0:C(X,2)=0:NEXTX:IFLV<>1THEN435
410 FORX=1TO30:FORY=1TO15:J(X,Y)=EM:NEXT:NEXT
415 FORX=1TO100:J(RND(30),RND(15))=TR:J(RND(30),RND(15))=BU:IFX>96THENJ(RND(30),RND(15))=GO
420 NEXT:PX=4:PY=4:J(4,4)=EM:FORX=1TO5:P(X,1)=RND(20)+10:P(X,2)=RND(15):J(P(X,1),P(X,2))=EM:NEXT:P1=5:WI=5:RETURN
435 C1=0:P1=0:FORX=1TO15:READL$:FORY=1TO30:ONK(ASC(MID$(L$,Y,1)))GOTO449,442,441,443,439,440,452,450,447,446,444,445,448
439 J(Y,X)=WA:GOTO454
440 J(Y,X)=RU:GOTO454
441 J(Y,X)=TR:GOTO454
442 J(Y,X)=EM:GOTO454
443 J(Y,X)=BU:GOTO454
444 J(Y,X)=GU:GOTO454
445 J(Y,X)=GO:GOTO454
446 J(Y,X)=AB:GOTO454
447 J(Y,X)=JE:GOTO454
448 J(Y,X)=ID:GOTO454
449 PX=Y:PY=X:J(Y,X)=EM:GOTO454
450 C1=C1+1:C(C1,1)=Y:C(C1,2)=X:J(Y,X)=EM:GOTO454
452 P1=P1+1:P(P1,1)=Y:P(P1,2)=X:J(Y,X)=EM
454 NEXT:NEXT:WI=C1+P1:RETURN
459 REM MOVEENEMIES
460 FORX=1TOP1:IFP(X,1)=0THENNEXT:GOTO484
461 IFRND(10)<5THENNEXT:GOTO484
463 XM=RND(3)-2:YM=RND(3)-2:IFXMANDYMTHENONRND(2)GOSUB8,9
468 NX=P(X,1)+XM:NY=P(X,2)+YM:V=16384+32*NY+NX:ONK(PEEK(V))GOTO145,475:NEXT:GOTO484
475 POKE16384+32*P(X,2)+P(X,1),EM:P(X,1)=NX:P(X,2)=NY:POKEV,PL:NEXT
484 FORX=1TOC1:XM=0:YM=0:IFC(X,1)=0THENNEXT:RETURN
485 XM=(C(X,1)>PX)-(C(X,1)<PX):YM=(C(X,2)>PY)-(C(X,2)<PY):IFXMANDYMTHENONRND(2)GOSUB8,9
496 NX=C(X,1)+XM:NY=C(X,2)+YM:V=16384+32*NY+NX:ONK(PEEK(V))GOTO140,505:NEXT:RETURN
505 POKE16384+32*C(X,2)+C(X,1),EM:C(X,2)=NY:C(X,1)=NX:POKEV,CA:NEXT:RETURN
515 REM NEXTLEVEL
520 CLS:PRINT@6*32,;:LV=LV+1:ONLV-1GOTO525,535,545,555
525 A$="RAOUL HEADS EASTWARD,":GOSUB138
528 PRINT:A$="TOWARD THE FORBIDDING SOUND":GOSUB138:PRINT:A$="OF BEATING DRUMS...":GOSUB138:GOTO565
535 A$="RAOUL ENTERS A CANNIBAL":GOSUB138:PRINT:A$="VILLAGE, CRUDE HUTS SURROUNDING":GOSUB138
539 PRINT:A$="A WEATHERED STONE ALTAR...":GOSUB138:GOTO565
545 PRINT@4*32,;:A$="RAOUL EMERGES FROM THE VILLAGE":GOSUB138:PRINT:A$="TO FIND THE ENTRANCE TO WHAT":GOSUB138
548 PRINT:A$="MUST SURELY BE THE RESTING PLACE":GOSUB138:A$="OF THE FABLED SAPPHIRE AMPERSAND":GOSUB138:GOTO565
555 A$="RAOUL ENTERS THE ANCIENT TEMPLE,":GOSUB138:A$="OBLIVIOUS OF THE DANGERS WITHIN.":GOSUB138
559 A$="THE AMPERSAND IS CLOSE BY...":GOSUB138
565 GOSUB405:CLS0:GOSUB180:RETURN
570 PRINT@488,"PRESS ANY KEY...";:FORX=1TO15:A$=INKEY$:NEXT
572 A$=INKEY$:IFA$=""THEN572
573 RETURN
574 REM PLAYERMOVES
580 A$=INKEY$:IFA$=""THEN580
587 IFA$=UP$THENXM=0:YM=-1:IFPY>1THEN600
588 IFA$=DN$THENXM=0:YM=1:IFPY<15THEN600
589 IFA$=RT$THENXM=1:YM=0:GOTO600
590 IFA$=LT$THENXM=-1:YM=0:GOTO600
591 IFA$=" "THENRETURN
592 IFA$="M"THENGOSUB630:RETURN
593 IFA$="F"THENGOSUB655:RETURN
594 IFA$="L"THENIFLV<>5THENGOSUB520:RETURN
596 GOTO580
600 NX=PX+XM:NY=PY+YM:V=16384+32*NY+NX:ONK(PEEK(V))GOTO623,623,624,624,624,624,147,146,615,619,617,613,750:RETURN
613 SC=SC+10+RND(15):PRINT@5,SC;:GOTO623
615 SC=SC+20+RND(10):PRINT@5,SC;:GOTO623
617 GG=1:PRINT@29,GG;:J(NX,NY)=EM
619 AM=AM+3:PRINT@17,AM;:J(NX,NY)=EM
623 POKE16384+32*PY+PX,EM:PX=NX:PY=NY:POKEV,RA:J(PX,PY)=EM
624 RETURN
627 REM MACHETE
630 A$=INKEY$:IFA$=""THEN630
632 V=0
633 IFA$=UP$THENV=1:XM=0:YM=-1
634 IFA$=DN$THENV=1:XM=0:YM=1
635 IFA$=RT$THENV=1:XM=1:YM=0
636 IFA$=LT$THENV=1:XM=-1:YM=0
637 NX=PX+XM
638 NY=PY+YM
639 IFNX<1ORNX>30THENV=0
640 IFNY<1ORNY>15THENV=0
641 IFV=0THENV=1:RETURN
642 IFJ(PX+XM,PY+YM)=BUTHENJ(PX+XM,PY+YM)=EM:POKE16384+32*(PY+YM)+PX+XM,EM
647 FORX=1TO8:IFNX=P(X,1)ANDNY=P(X,2)THENPOKE16384+32*P(X,2)+P(X,1),EM:P(X,1)=0:SC=SC+30:PRINT@5,SC;:WI=WI-1
650 NEXT:RETURN
654 REM SHOOT
655 IFGG=0ORAM=0THENRETURN
656 AM=AM-1:PRINT@17,AM;
660 A$=INKEY$:IFA$=""THEN660
665 IFA$=UP$THENXM=0:YM=-1:GOTO670
666 IFA$=DN$THENXM=0:YM=1:GOTO670
667 IFA$=RT$THENXM=1:YM=0:GOTO670
668 IFA$=LT$THENXM=-1:YM=0:GOTO670
669 GOTO660
670 NX=PX:NY=PY:SA=0
675 NX=NX+XM:NY=NY+YM:IFNX=0ORNX=31ORNY=0ORNY=16THEN690
677 IFJ(NX,NY)<>EMTHEN690
678 POKE16384+32*NY+NX,46:FORX=1TO8:IFNX=C(X,1)ANDNY=C(X,2)THENC(X,1)=0:POKE16384+32*NY+NX,EM:SA=50:WI=WI-1:GOTO690
685 IFNX=P(X,1)ANDNY=P(X,2)THENP(X,1)=0:POKE16384+32*NY+NX,EM:SA=20:WI=WI-1:GOTO690
687 NEXT:POKE16384+32*NY+NX,EM:GOTO675
690 IFSA<>0THENSC=SC+SA:PRINT@5,SC;
698 RETURN
700 GOSUB130
715 CLS:PRINT:PRINT:PRINT:ONMDGOTO717,720
717 A$="RAOUL IS SAVAGELY BEHEADED,":GOSUB138
718 A$="CHOPPED INTO PIECES, AND LATER":GOSUB138
719 A$="EATEN BY THE INDIGENT CANNIBALS.":GOSUB138:GOTO724
720 A$="RAOUL IS STRANGLED AND ABSORBED":GOSUB138
723 A$="BY A FIERCE CARNIVOROUS PLANT.":GOSUB138
724 A$="HIS BONES LITTER THE GROUND, A":GOSUB138
725 A$="SINISTER TESTAMENT TO HIS":GOSUB138
726 A$="FOOLISHNESS.":GOSUB138:GOTO770
743 REM SUB YOUWIN
750 CLS:PRINT
756 A$="YOU'VE DONE IT!":GOSUB138
757 A$="YOU HAVE BRAVED THE TERRORS":GOSUB138
758 A$="OF THE JUNGLE, AND EMERGED":GOSUB138
759 A$="TRIUMPHANT, WITH THE ":GOSUB138
760 A$="FABLED SAPPHIRE AMPERSAND":GOSUB138
761 A$="IN HAND.":GOSUB138
763 A$="--------------------":GOSUB138
765 A$="- CONGRATULATIONS! -":GOSUB138
767 A$="--------------------":GOSUB138
770 PRINT:A$="YOUR FINAL SCORE:"+STR$(SC)+" PTS.":GOSUB138
794 PRINT:A$="THANKS FOR PLAYING!":GOSUB138
795 GOSUB570:RUN
1050 DATA "TTBTTTTTBTTT  C   TTBTBBTBBBBB"
1063 DATA "TTTT P TTBT     TTTTTTTTTBBBJB"
1064 DATA "TBTP G  TT     TTT     TTTBBBB"
1065 DATA "TTT    PTT    TTT $$  TTBTBBBB"
1066 DATA "TBTT P TT    TTTT   TTTT   BBB"
1067 DATA "TTBT  TT      TT   TTTC   BBBB"
1068 DATA "TTT  TT                  BBBBB"
1069 DATA "     T     TBBBBB    T     BBB"
1070 DATA " X       TTTBBBBTBC     T BBBB"
1071 DATA " TTTTT    TT        B BBB   BB"
1072 DATA " TTTTT   BBBBTT     TRWBWTB   "
1073 DATA " BTTT   BBBTTTT    TBR  WBTB  "
1074 DATA " B     BBTTTTT     BTW ARTB   "
1075 DATA "   $ TTTTTTTTTTTT   TRWRWB    "
1076 DATA "   TTTTTTTTTTTTTTTT  BBBB     "
1077 DATA "TTTBTBTTTBTTTTTTTTBTTTTTBTTTTT"
1078 DATA "TBBTTTTTTTTTT     TTTTBTTTTBTT"
1079 DATA "TTTTTT           BB    TTTTTBT"
1080 DATA "TT     BB       BAAB      PTTT"
1081 DATA "T  P  B$$B      BAAB        TT"
1082 DATA "T     BCC         B   BB     T"
1083 DATA "       BB     WW     BCAB     "
1084 DATA "X             WW      CCB    T"
1085 DATA "T        B            BB    TT"
1086 DATA "T       BA B    B           TT"
1087 DATA "BT      BJCB   BC B     P  TTT"
1088 DATA "TTT      BB    B$CB       TTTT"
1089 DATA "TBTTTT          BB      TTTBTT"
1090 DATA "TTTBBTTTTT   P     TTTTTTBTTBT"
1091 DATA "TTBTTBTTTTTTTTTTTTTBBTBTTTTBBT"
1092 DATA "TTTTTTTTTTTTTTTTBBBB  C   BT W"
1093 DATA "TTTTTTTTT  C  TTBBB      BT WW"
1094 DATA "TTTTTTT      TTTT   C B B TWW "
1095 DATA "TTTTTT      TTTT        B WW$T"
1096 DATA "TTT          TT     WWWWWWW BP"
1097 DATA "TT     TTT       C  WRWRWW PB "
1098 DATA "TTX   TTTT      BBBTWWWWW TP  "
1099 DATA "TTT     TTT    BBB BB  B  B  $"
1100 DATA "TTT             B BTWWWWW  T B"
1101 DATA "TTTT   TTT      C B WRWRWWTT  "
1102 DATA "TTTTT   TTTT        WWWWWWWB P"
1103 DATA "TTTTTTT    TTT       B   BWW$T"
1104 DATA "TTTTTTTTTT           B   TBWWB"
1105 DATA "TTTTTTTTTTTTTTBBBC     B  BTWW"
1106 DATA "TTTTTTTTTTTTTTTTBBBB     B  BW"
1107 DATA "BTBWWWWWWWWWWWWWWWWWWWWWWWWTBT"
1108 DATA "TBW     W$$$JW  C   W      WTB"
1109 DATA "TW   C   $$$JW      W       WT"
1110 DATA "WWW WWWWWWWWWWW W WWWWWWWWWWWW"
1111 DATA "W        P   W  W  WW   P    W"
1112 DATA "W  C  W      W  W WW     WWWWW"
1113 DATA "WWWWWWWWW  WWW WW W  C   W   W"
1114 DATA "X            W  W    C     I W"
1115 DATA "WWW WWWWW  WWW WWWW  C   W   W"
1116 DATA "W     W  AA  W    WW     WWWWW"
1117 DATA "W  C  W      W P   WW   P    W"
1118 DATA "WWWW WWWWWWWWWW  WWWWWWWWWWWWW"
1119 DATA "TW    P               C     WT"
1120 DATA "BTW     W    W      W      WBT"
1121 DATA "TBBWWWWWWWWWWWWWWWWWWWWWWWWTTB"
2000 REM 1996 ZACK JOHNSON
2010 REM MC-10 VER JIM GERRIE
